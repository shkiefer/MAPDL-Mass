ALLSEL

! define coordinate system to calculate mass properties about, my_cs is an 'apdl_name' or specific cs ID (integer)
cs_mass = my_cs

finish
! change filename to write output to mass.out to not overwrite default file.out
/filname, mass

fini
/prep7
    
! get cs location and angles
*GET,cs_x,CDSY,%cs_mass%,LOC,X
*GET,cs_y,CDSY,%cs_mass%,LOC,Y
*GET,cs_z,CDSY,%cs_mass%,LOC,Z
*GET,cs_xy,CDSY,%cs_mass%,ANG,XY
*GET,cs_yz,CDSY,%cs_mass%,ANG,YZ
*GET,cs_zx,CDSY,%cs_mass%,ANG,ZX
	
fini
/sol

! prepare element matrices / calculate mass properties
irlf,-1 
psolve,elform 
psolve,elprep 

! get translational mass
*get, mass_x, ELEM,0,MTOT,X
*get, mass_y, ELEM,0,MTOT,Y
*get, mass_z, ELEM,0,MTOT,Z

! get cg location
*get, cg_x, ELEM,0,MC,X
*get, cg_y, ELEM,0,MC,Y
*get, cg_z, ELEM,0,MC,Z

! get rotational inertia
*get, moim_x, ELEM,0,IMC,X
*get, moim_y, ELEM,0,IMC,Y
*get, moim_z, ELEM,0,IMC,Z
*get, moim_xy, ELEM,0,IMC,XY
*get, moim_yz, ELEM,0,IMC,YZ
*get, moim_zx, ELEM,0,IMC,ZX

! translate inertia components to cs_mass loc
xg = cg_x - cs_x
yg = cg_y - cs_y
zg = cg_z - cs_z
Ixx_cst = moim_x+mass_x*(yg**2+zg**2)
Iyy_cst = moim_y+mass_y*(xg**2+zg**2)
Izz_cst = moim_z+mass_z*(xg**2+yg**2)
Ixy_cst = moim_xy+mass_x*xg*yg
Iyz_cst = moim_yz+mass_y*yg*zg
Izx_cst = moim_zx+mass_z*zg*xg
    
! create APDL 2D array
*dim,I_cst, array,3,3
I_cst(1,1)=Ixx_cst
I_cst(1,2)=Ixy_cst
I_cst(1,3)=Izx_cst
I_cst(2,1)=Ixy_cst
I_cst(2,2)=Iyy_cst
I_cst(2,3)=Iyz_cst
I_cst(3,1)=Izx_cst
I_cst(3,2)=Iyz_cst
I_cst(3,3)=Izz_cst
    
! rotate inertia componets to cs_mass angles
! https://www.simutechgroup.com/tips-and-tricks/fea-articles/286-fea-tips-tricks-ansys-rotation-convention

! make transformation matrix T
PI=ACOS(-1)
c1=COS(cs_xy*PI/180.)
c2=COS(cs_yz*PI/180.)
c3=COS(cs_zx*PI/180.)
s1=SIN(cs_xy*PI/180.)
s2=SIN(cs_yz*PI/180.)
s3=SIN(cs_zx*PI/180.)
    
txx = c1*c3-s1*s2*s3
txy = s1*c3+c1*s2*s3
txz = -s3*c2
tyx = -s1*c2
tyy = c1*c2
tyz = s2
tzx = c1*s3+s1*s2*c3
tzy = s1*s3-c1*c3*s2
tzz = c2*c3
    
! create APDL 2D array
*dim,Tr, array,3,3
Tr(1,1)=txx
Tr(1,2)=txy
Tr(1,3)=txz
Tr(2,1)=tyx
Tr(2,2)=tyy
Tr(2,3)=tyz
Tr(3,1)=tzx
Tr(3,2)=tzy
Tr(3,3)=tzz

! copy matrices from APDL to 'APDL Math'
*DMAT, I_cstm, D, IMPORT, APDL, I_cst
*DMAT, Trm, D, IMPORT, APDL, Tr

! multiply matrices using 'APDL math'
*MULT,Trm,,I_cstm,, M3
*MULT,M3,,Trm,TRANS,I_csm

! copy result back to APDL
*EXPORT,I_csm,APDL,I_cs,1,1

! use default 'my_' prefix to show results in Mechanical command snippet properties
my_mass_x = mass_x
my_mass_y = mass_y
my_mass_y = mass_z
my_Ixx = I_csm(1,1)
my_Iyy = I_csm(2,2)
my_Izz = I_csm(3,3)
my_Ixy = I_csm(1,2)
my_Ixz = I_csm(1,3)
my_Iyx = I_csm(2,1)
my_Iyz = I_csm(2,3)
my_Izx = I_csm(3,1)
my_Izy = I_csm(3,2)

finish
/filname
/post1
